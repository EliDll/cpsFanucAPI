PROGRAM remotemove

%COMMENT = ' Remote Move'
%NOLOCKGROUP
%NOPAUSE=ERROR+COMMAND+TPENABLE
%NOBUSYLAMP
	
VAR

	response_file :FILE
	STATUS: INTEGER
	
	x: STRING[127]
	y: STRING[127]
	z: STRING[127]
	w: STRING[127]
	p: STRING[127]
	r: STRING[127]
	linear_path: STRING[127]
	interrupt: STRING[127]

	coords_6d: ARRAY[6] OF REAL
	linear_flag: BOOLEAN
	interrupt_flag: BOOLEAN

	%INCLUDE ../util/generic_util
	%INCLUDE ../core/core_move

BEGIN	
	OPEN FILE response_file ('RW', 'RD:RESPONSE.HTM')
	STATUS = IO_STATUS(response_file)
	IF (STATUS <> 0) THEN
		RETURN
	ENDIF
	
	-- check url params (bool flags can be omitted)
	IF (UNINIT(x) OR 
		UNINIT(y) OR 
		UNINIT(z) OR 
		UNINIT(w) OR 
		UNINIT(p) OR 
		UNINIT(r)) THEN
		WRITE response_file ( '{"result":"failed", "reason":','uninitialized parameter','}') 
		RETURN
	ELSE
		IF ((x = '') OR
			(y = '') OR
			(z = '') OR
			(w = '') OR
			(p = '') OR
			(r = ''))THEN
			WRITE response_file ( '{"result":"failed", "reason":','empty parameter','}') 
			RETURN
		ENDIF
	ENDIF
	
	-- convert the positions to real numbers
	CNV_STR_REAL(x, coords_6d[1])
	CNV_STR_REAL(y, coords_6d[2])
	CNV_STR_REAL(z, coords_6d[3])
	CNV_STR_REAL(w, coords_6d[4])
	CNV_STR_REAL(p, coords_6d[5])
	CNV_STR_REAL(r, coords_6d[6])
	
	-- parse optional paramters or set to default
	IF(UNINIT(linear_path)) THEN
		linear_flag = TRUE
	ELSE
		linear_flag = STR_TO_BOOL(linear_path)
	ENDIF
	
	IF(UNINIT(interrupt)) THEN
		interrupt_flag = FALSE
	ELSE
		interrupt_flag = STR_TO_BOOL(interrupt)
	ENDIF
		
	CORE_MOVE(coords_6d, linear_flag, interrupt_flag, response_file)

	CLOSE FILE response_file
END remotemove