-- %COMMENT = 'Parse list of struct tags'

ROUTINE  parse_varlist(xml_file: FILE)

CONST 
	list_tag = 3
	struct_tag = 4


VAR
	structs: ARRAY [32] OF struct_t
	tag_id: INTEGER -- id of last scanned tag
	tag_string: STRING[32] -- last scanned tagname
	func_code: INTEGER -- tag type of last scan

	status: INTEGER -- function return status storage

	start_list: BOOLEAN
	end_list: BOOLEAN

	next_index: INTEGER
	i: INTEGER

BEGIN
WRITE log('PARSER BEGIN', CR)
	
SET_FILE_ATR (xml_file, ATR_XML)
CLR_IO_STAT(xml_file)

OPEN FILE xml_file('RO', 'PIP:read.dt') -- reopen as read only after setting xml attr
-- OPEN FILE xml_file ('RO', 'MC:nested_list.xml') -- read only
status = IO_STATUS(xml_file)

-- register tag to be detected by scan
XML_ADDTAG(xml_file, 'list_tag', 16, FALSE, list_tag, status)
XML_ADDTAG(xml_file, 'struct_tag', 16, FALSE,struct_tag, status)

start_list = FALSE
end_list = FALSE
next_index = 1

-- scan until list start tag is found
WHILE (start_list = FALSE) DO
	XML_SCAN(xml_file, tag_string, tag_id, func_code, status)
	IF ( status = 0) THEN
		WRITE log( 'EOF', CR)
		start_list = TRUE
	ENDIF
	IF (status = XML_FUNCTION) THEN
		IF (tag_id = list_tag) THEN
		WRITE log('found list start', CR)
		start_list = TRUE
		ENDIF
	ELSE
	WRITE log('ERR', status, CR)
	ABORT
	ENDIF
ENDWHILE

-- scan list elements until end tag is found
WHILE ((end_list = FALSE) AND (next_index <= 32)) DO
	XML_SCAN(xml_file, tag_string, tag_id, func_code, status)
	IF ( status = 0) THEN
		WRITE log( 'EOF', CR)
		end_list = TRUE
	ENDIF
	IF (status = XML_FUNCTION) THEN
		SELECT tag_id OF
			CASE(list_tag):
				WRITE log('found list end', CR)
				end_list = TRUE
			CASE(struct_tag):
				WRITE log('adding struct: ', next_index, CR)
				-- XML_SETVAR(FILE, 'PROGRAM_NAME', 'VAR_NAME', status) 
				XML_SETVAR(xml_file, 'xml_client', 'buffer_struct', status) 
				structs[next_index] = buffer_struct
				next_index = next_index + 1
		ENDSELECT
	ELSE
	WRITE log('ERR', status, CR)
	ABORT
	ENDIF
ENDWHILE

FOR i = 1 TO (next_index -1) DO
		WRITE log('Struct ', i, CR)
		WRITE log('	int_val:', structs[i].int_val, '  ')
		WRITE log('	float_val:', structs[i].float_val, '  ')
		WRITE log('	string_val:', structs[i].string_val, CR)
ENDFOR

WRITE log('PARSER END', CR)
END parse_varlist