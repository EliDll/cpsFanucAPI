PROGRAM k_sktMsg_c

%NOLOCKGROUP
%NOBUSYLAMP

CONST
    TP_WIDTH = 40
VAR 
	exFile : FILE

	serverTag : STRING[8]
	counter : INTEGER
	serverMsg : STRING[254]
	nBytes : INTEGER
	nBytes2 : INTEGER
	listenToSer : BOOLEAN
	entry : INTEGER
	STATUS : INTEGER

ROUTINE int_to_strg(int : INTEGER) : STRING FROM klutils

ROUTINE checkError(operation : STRING)
BEGIN
    IF STATUS <> 0 THEN
        WRITE TPDISPLAY('ERROR:  ' + operation,CR)
        STATUS = 0
    ENDIF
END checkError

ROUTINE wrapPrint(wrapPrintText: STRING)
VAR
    len : INTEGER
    printIdx : INTEGER
    subPrintStr : STRING[TP_WIDTH]
BEGIN
    
    len = STR_LEN(wrapPrintText)
    printIdx = 1
    WHILE printIdx <= len DO
        subPrintStr = SUB_STR(wrapPrintText, printIdx, TP_WIDTH)
        WRITE TPDISPLAY(subPrintStr, CR)
        printIdx = printIdx + TP_WIDTH
    ENDWHILE
END wrapPrint

ROUTINE connectServ
BEGIN
    listenToSer = TRUE
    
    serverTag = 'C3:'
    
    MSG_DISCO(serverTag, STATUS)
    MSG_CONNECT(serverTag, STATUS)
    
    checkError('Connecting to Server')

    -- Open File
    OPEN FILE exFile('RW', serverTag)
    
    STATUS = IO_STATUS(exFile)
    
    checkError('Opening File.')

    WRITE TPDISPLAY('***Finished Connecting***',CR)

    WRITE exFile('GET /robot_position/?format=json', CR, CR)
    WRITE TPDISPLAY('***Sent Get Request***',CR)
        
END connectServ

ROUTINE rcvServer
BEGIN
    WHILE listenToSer DO
        REPEAT
            BYTES_AHEAD(exFile, nBytes, STATUS)           
            IF STATUS <> 0 THEN
                nBytes = -1
                STATUS = 0
            ENDIF
            
            DELAY 100
                
        UNTIL nBytes > 0
        
        IF nBytes >= 1 THEN
            READ exFile(serverMsg::nBytes)
            STATUS = IO_STATUS(exFile)
            
            checkError('I/O_Status (File)')
            
            wrapPrint(serverMsg)
        ENDIF
    ENDWHILE
END rcvServer
    
    

BEGIN
	WRITE(CHR(128))

	connectServ
	WRITE TPDISPLAY('***',CR)
	rcvServer
	WRITE TPDISPLAY('***',CR)

END k_sktMsg_c
