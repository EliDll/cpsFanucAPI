PROGRAM k_sktMsg_c

%NOLOCKGROUP
%NOBUSYLAMP

VAR
	connFile : FILE
	ramFile : FILE
	STATUS : INTEGER

%INCLUDE ../util/util

ROUTINE connectServ(serverTag : STRING)
BEGIN
	MSG_DISCO(serverTag, STATUS)
	MSG_CONNECT(serverTag, STATUS)

	checkError('Connecting to Server', STATUS)

	-- Open File
	OPEN FILE connFile('RW', serverTag)

	STATUS = IO_STATUS(connFile)

	checkError('Opening File.', STATUS)
END connectServ

ROUTINE requestData : INTEGER
VAR
	nBytes : INTEGER
	serverMsg : STRING[254]
BEGIN
	WRITE connFile('GET /robot_position/?format=json', CR, CR)
	REPEAT
		BYTES_AHEAD(connFile, nBytes, STATUS)           
		IF STATUS <> 0 THEN
			RETURN (-1)
		ENDIF
		DELAY 100
	UNTIL nBytes > 0
    
	WHILE nBytes >= 1 DO
		READ connFile(serverMsg::nBytes)
		STATUS = IO_STATUS(connFile)

		checkError('I/O_Status (File)', STATUS)

		wrapPrint(serverMsg)
		
		BYTES_AHEAD(connFile, nBytes, STATUS)           
		IF STATUS <> 0 THEN
			RETURN (-1)
		ENDIF
		
	ENDWHILE
	RETURN (0)
END requestData
   
BEGIN
	connectServ('C3:')
	STATUS  = requestData
	checkError('requestData', STATUS)
	
	CLOSE FILE connFile

END k_sktMsg_c
